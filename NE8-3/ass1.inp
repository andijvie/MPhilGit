set title "UO2 PIN MODEL"

% --- Define materials --- %

% - UO2 3.5%w
mat fuel	sum
92235.09c	-0.320862646119516  % [g/cm]
92238.09c   -8.846641528723795  % [g/cm]
8016.09c    -1.232495825156688  % [g/cm]

mat clad    sum
40000.06c	-6.56  % [g/cm] (from NE MPhil databook)

mat coolant	sum moder lwtr 1001 
1001.06c -0.07457895238987516  % [g/cm]
8016.06c -0.59181104761012480  % [g/cm]

% thermal scattering library for H20 
therm lwtr lwj3.11t 

% --- Define geometry --- %
surf 1000 sqc 0.0 0.0 1.260  % [cm] 

pin 1
fuel 0.4750  % [cm]
clad 0.5405  % [cm]
coolant

cell insideCell  0 fill 1 -1000
cell outsideCell 0 outside 1000

% --- Neutron population and criticality cycles:
 
set pop 10000 1000 100  % [#neutrons], [#active cycles], [#inactive cycles]

% --- Boundary conditions: 
set bc 2  % reflective

% --- Normalisation:
set powdens 0.038 fuel  % [kW/gHM]

% --- Geometry and mesh plots: 
 
plot 3 500 500 
mesh 3 500 500 
 
% --- Cross section library file path: 
set acelib "/usr/software/mcnplib/SERPENT/XSdata_endfb7/sss_endfb7u.xsdata"

% --- Detectors --- %

% *Example* detector for U235 fission rate

% Define a fake material so that we only score on U235
mat just235 1.0 92235.09c 1.0

% Define detector for U235 fission rate
det exampleDet dm fuel dr 18 just235

% --- ASSIGNMENT 1
% Using appropriate detectors, calculate radial distribution of U238 capture reaction rate in the
% fuel pellet. Plot the U238 capture rate versus radial distance from the fuel centre using 10
% equal-volume radial regions.

% --- Detector that calculates the radial capture distribution in the fuel:
%     Name of the detector is ASSIGNMENT1.
%     of the material at the interaction site (keyword: fuel)
%     The detector uses response number -2 (total capture cross section)
%     Detector only scores interactions at material "just238" ("dm just238").

mat just238 1.0 92238.09c 1.0
det ASSIGNMENT1macro dm fuel  % Calculate reaction rates in material fuel
                     dr -2 just238  % Total macroscopic capture rate
                     dn 4 10 1 1  % Detector is uneven-cylindrical (type 4), with 10 radial bins (only 1 angular and axial bin)
                     0.000000 0.150208 0.212426 0.260168 0.300416 0.335876 0.367933 0.397414 0.424853 0.450625 0.475000 % [cm] radii 
                     0.0 360.0   % [deg] 1 bin for all the angles
                     -1E18 1E18  % 1 bin for all the heights

det ASSIGNMENT1micro dm fuel  % Calculate reaction rates in material fuel
                     dr 102 just238  % Total macroscopic capture rate
                     dn 4 10 1 1  % Detector is uneven-cylindrical (type 4), with 10 radial bins (only 1 angular and axial bin)
                     0.000000 0.150208 0.212426 0.260168 0.300416 0.335876 0.367933 0.397414 0.424853 0.450625 0.475000 % [cm] radii 
                     0.0 360.0   % [deg] 1 bin for all the angles
                     -1E18 1E18  % 1 bin for all the heights



